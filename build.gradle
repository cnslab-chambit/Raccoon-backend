plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.epages.restdocs-api-spec' version '0.16.2'
	id 'com.ewerk.gradle.plugins.querydsl' version '1.0.10'
}

repositories {
	mavenCentral()
}

bootJar {
	enabled = false
}

jar {
	enabled = true
}

subprojects{
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'kwu'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	ext {
		springCloudVersion = "2021.0.1"
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	dependencies {
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		implementation 'org.springframework.boot:spring-boot-starter-aop'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'
		implementation group: 'it.ozimov', name: 'embedded-redis', version: '0.7.1'
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

}

project(':raccoon-api'){
	apply plugin: 'com.epages.restdocs-api-spec'


	dependencies {
		implementation project(path:':raccoon-common')
		implementation project(path:':raccoon-domain')
		implementation 'org.springframework.boot:spring-boot-starter-security'
		implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
		implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'


		runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.2'
		runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.2'

		testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
		testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.16.2'
		testImplementation 'org.springframework.security:spring-security-test'
	}

	task copyTest {
		dependsOn("openapi3")
		copy {
			from "$buildDir/api-spec/openapi3.yaml"
			into "src/main/resources/static/docs/."
		}
	}

	openapi3 {
		server = 'http://localhost:8080' // 자신 서버의 URL 작성
		title = 'RestDocs to Swagger 변환 테스트' // Swagger 작동시 페이지에 나오는 제목
		description = 'Restdocs로 API 문서 작성 후 이를 Swagger로 변환하는 페이지' // Swagger 페이지 제목 밑에 설명란에 추가되는 메세지
		version = '0.0.1-SNAPSHOT' // 애플리케이션 버전 정보
		format = 'yaml' // json으로도 가능
	}

}
project(':raccoon-domain'){

	apply plugin: 'com.ewerk.gradle.plugins.querydsl'

	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}

	configurations {
		querydsl {
			extendsFrom compileClasspath
		}
	}

	ext {
		queryDslVersion = "5.0.0"
	}

	dependencies {
		implementation project(path:':raccoon-common')
		implementation project(path:':raccoon-infra')
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'mysql:mysql-connector-java'
		implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
		implementation "com.querydsl:querydsl-apt:${queryDslVersion}"
	}

	def querydslSrcDir = "$buildDir/generated/querydsl"

	querydsl {
		jpa = true
		querydslSourcesDir = querydslSrcDir
	}

	sourceSets {
		main.java.srcDir querydslSrcDir
	}

	compileQuerydsl {
		options.annotationProcessorPath = configurations.querydsl
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
		querydsl.extendsFrom compileClasspath
	}

}

project(':raccoon-infra'){
	bootJar {
		enabled = false
	}

	jar {
		enabled = true
	}
	dependencies {
		implementation project(path:':raccoon-common')
		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
		implementation 'com.amazonaws:aws-java-sdk-s3:1.12.362'
		implementation group: 'org.imgscalr', name: 'imgscalr-lib', version: '4.2'
		implementation 'org.apache.commons:commons-lang3:3.5'

	}
}

project(':raccoon-common'){
	bootJar {
		enabled = false
	}
	jar {
		enabled = true
	}
	dependencies {
	}
}